plugins {
    id "com.github.hierynomus.license" version "0.15.0"
}

apply plugin: 'groovy'
apply plugin: 'java'

version = '1.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

//create a single Jar with all dependencies
task buildJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Proxeus Document Service',
                'Implementation-Version': version,
                'Main-Class': 'com.proxeus.Main'
    }
    archiveName = "document-service.jar"
    destinationDir = file("$rootDir/")
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA', '**/*Test.java', '**/*.xsd', '**/*.cat', '**/*.txt'
}

repositories {
    mavenCentral()
    maven {
        url "http://repository.jspresso.org/maven2"
    }
    maven {
        url "https://jcenter.bintray.com/"
    }
    maven {
        url "https://dl.bintray.com/groovy/maven"
    }
}

dependencies {
    compile 'com.google.guava:guava:18.0'
    compile 'javax.servlet:javax.servlet-api:3.1.0'
    compile 'org.eclipse.jetty:jetty-io:9.4.18.v20190429'
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.9'
    def ex = {
        exclude group: 'org.slf4j', module: 'slf4j-jdk14'
        exclude group: 'org.slf4j', module: 'slf4j-api'
        exclude group: 'org.slf4j', module: 'slf4j-nop'
        exclude group: 'org.slf4j', module: 'jcl-over-slf4j'
    }
    compile "org.slf4j:slf4j-simple:1.7.25"
    compile "log4j:log4j:1.2.17", ex
    compile 'org.codehaus.groovy:groovy-all:2.3.11', ex

    compile "org.jtwig:jtwig-core:5.87.0.RELEASE", ex

    compile "javax.xml.bind:jaxb-api:2.2.11", ex
    compile "commons-io:commons-io:RELEASE", ex
    compile "javaxt:javaxt-core:1.7.8", ex
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'

    compile "commons-codec:commons-codec:1.11", ex
    compile "commons-io:commons-io:2.5", ex
    compile "commons-fileupload:commons-fileupload:1.3.3", ex
    compile group: 'org.apache.commons', name: 'commons-text', version: '1.6', ex

    compile "com.fasterxml.jackson.core:jackson-databind:2.9.9", ex
    compile "com.sparkjava:spark-core:2.9.1", ex

    compile "org.libreoffice:unoil:6.2.3", ex
    compile "org.libreoffice:jurt:6.2.3", ex
    compile "org.libreoffice:juh:6.2.3", ex
    compile "org.libreoffice:ridl:6.2.3", ex

    testCompile "junit:junit:4.10"
}
